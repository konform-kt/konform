buildscript {
    ext.projectVersion = '0.3.0-RC1'
    ext.projectName = 'konform'
    ext.projectGroup = 'io.konform'
    ext.projectDesc = 'Konform: Portable validations for Kotlin'
    ext.projectHost = 'github'
    ext.projectOrg = 'konform-kt'
    ext.projectLicense = 'MIT'
    ext.projectLicenseUrl = 'http://opensource.org/licenses/MIT'
    ext.projectDevelNick = 'nlochschmidt'
    ext.projectDevelName = 'Niklas Lochschmidt'
    ext.projectInceptionYear = 2018

    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.4.32'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

group projectGroup
version projectVersion

apply plugin: 'maven-publish'

kotlin {
    jvm()
    js(BOTH) {
        browser {
            testTask {
                useKarma {
                    // I cannot get chrome headless to work with karma on any gradle project on a mac
                    // It just times out trying to start chrome (which I have installed). Security thing probably
                    // phantomJS works fine.
                    // useChromeHeadless()
                    usePhantomJS()
                }
            }        }
        nodejs {
        }
    }
    sourceSets {
        commonMain {}
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {}
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {}
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

task stubSources(type: Jar) {
    classifier 'sources'
}

task stubJavadoc(type: Jar) {
    classifier 'javadoc'
}

afterEvaluate {
    publishing {
        publications.all {
            if (name == 'kotlinMultiplatform') {
                artifact(stubJavadoc)
                artifact(stubSources)
            } else {
                artifact(stubJavadoc)
            }
            logger.info("Artifact id = ${it.artifactId}")
            pom {
                name = projectName
                description = projectDesc
                url = 'https://github.com/konform-kt/konform'
                licenses {
                    license {
                        name = projectLicense
                        url = projectLicenseUrl
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = projectDevelNick
                        name = projectDevelName
                    }
                }

                scm {
                    url = "https://github.com/konform-kt/konform.git"
                }
            }
        }
    }
}

publishing {
    repositories {
        maven {
            def user = 'konform-kt'
            def repo = 'konform'
            def name = 'konform'
            url = "https://api.bintray.com/maven/$user/$repo/$name/;publish=0"
            credentials {
                username = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
                password = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
            }
        }
    }
}
