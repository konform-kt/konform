buildscript {
    ext.projectVersion = '0.4.0'
    ext.projectName = 'konform'
    ext.projectGroup = 'io.konform'
    ext.projectDesc = 'Konform: Portable validations for Kotlin'
    ext.projectHost = 'github'
    ext.projectOrg = 'konform-kt'
    ext.projectLicense = 'MIT'
    ext.projectLicenseUrl = 'http://opensource.org/licenses/MIT'
    ext.projectDevelNick = 'nlochschmidt'
    ext.projectDevelName = 'Niklas Lochschmidt'
    ext.projectInceptionYear = 2018
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.7.10'
    id 'maven-publish'
    id 'signing'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

repositories {
    mavenCentral()
}

group projectGroup
version projectVersion


kotlin {
    sourceSets.all {
        languageSettings {
            languageVersion = "1.5" // Change to 1.6 when switching to Kotlin 1.8
            apiVersion = "1.5"      // Change to 1.6 when switching to Kotlin 1.8
        }
    }
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '1.8'
        }
        withJava()
        testRuns["test"].executionTask.configure {
            useJUnitPlatform()
        }
    }
    js(BOTH) {
        browser()
        nodejs()
    }
    sourceSets {
        commonMain {
            dependencies {
                compileOnly kotlin('stdlib')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test')
            }
        }
        jvmMain {
            dependencies {
                compileOnly kotlin('stdlib-jdk8')
            }
        }
        jvmTest {}
        jsMain {}
        jsTest {}
    }
}

task stubJavadoc(type: Jar) {
    classifier 'javadoc'
}

afterEvaluate {
    publishing {
        publications.all {
            artifact(stubJavadoc)
            logger.info("Artifact id = ${it.artifactId}")
            pom {
                name = projectName
                description = projectDesc
                url = 'https://github.com/konform-kt/konform'
                licenses {
                    license {
                        name = projectLicense
                        url = projectLicenseUrl
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = projectDevelNick
                        name = projectDevelName
                    }
                }

                scm {
                    url = "https://github.com/konform-kt/konform.git"
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}

nexusPublishing {
    repositories {
        sonatype()
    }
}
